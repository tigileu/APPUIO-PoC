buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'

task stage {
    dependsOn clean, build
}

task npmBuild(type: NpmTask) {
    args = ["run","build:prod"]
}

npmBuild.dependsOn npmInstall

processResources.dependsOn npmBuild

node {
    version = '6.5.0'
    npmVersion = '3.10.3'
    download = true
    nodeModulesDir = file("${project.projectDir}")
}

jar {
    baseName = 'cle-springboot-angular'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.hsqldb:hsqldb")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-devtools")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}
